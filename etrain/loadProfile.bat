rem @ECHO OFF
set PROFILE=default
if not [%1]==[] set PROFILE=%1
echo Setting environment specific properties using profile %PROFILE%
set PROFILE_FILE=%~p0profiles\%PROFILE%.properties
if not exist %PROFILE_FILE% exit /B 1
for /F "eol=# tokens=1,* delims==" %%g in (%PROFILE_FILE%) do (set %%g=%%h)
set /A ADMIN_PORT=%PORT_BASE% + 48
set /A ACTIVEMQ_PORT=%PORT_BASE% + 56
set ACTIVEMQ_REMOTE_CONNECTOR=tcp://%ACTIVEMQ_REMOTE_CONNECTOR_HOST%:%ACTIVEMQ_PORT%
set /A JMX_PORT=%PORT_BASE% + 86


rem *********************
rem normalize all paths
rem *********************

set SCRIPT_DIR=%~dp0
set SCRIPT_DIR=%SCRIPT_DIR:\=\\%
if x%DOMAIN_DIR::\=%==x%DOMAIN_DIR% set DOMAIN_DIR=%SCRIPT_DIR%%DOMAIN_DIR%
if x%APPLICATION::\=%==x%APPLICATION% set APPLICATION=%SCRIPT_DIR%%APPLICATION%
if x%ACTIVEMQ_RAR::\=%==x%ACTIVEMQ_RAR% set ACTIVEMQ_RAR=%SCRIPT_DIR%%ACTIVEMQ_RAR%
if x%ACTIVEMQ_STORAGE_DIR::\=%==x%ACTIVEMQ_STORAGE_DIR% set ACTIVEMQ_STORAGE_DIR=%SCRIPT_DIR%%ACTIVEMQ_STORAGE_DIR%
if x%ACTIVEMQ_BROKER_CONFIG::\=%==x%ACTIVEMQ_BROKER_CONFIG% set ACTIVEMQ_BROKER_CONFIG=%SCRIPT_DIR%%ACTIVEMQ_BROKER_CONFIG%
if x%WMS_LOGBACK_XML::\=%==x%WMS_LOGBACK_XML% set WMS_LOGBACK_XML=%SCRIPT_DIR%%WMS_LOGBACK_XML%
if x%WMS_CONFIG_DIR::\=%==x%WMS_CONFIG_DIR% set WMS_CONFIG_DIR=%SCRIPT_DIR%%WMS_CONFIG_DIR%
set WMS_JDBC_URL=%WMS_JDBC_URL::=\\:%
set WMS_LOGBACK_XML=%WMS_LOGBACK_XML:\\=\\\\%
set WMS_CONFIG_DIR=%WMS_CONFIG_DIR:\\=\\\\%

rem ACTIVEMQ_BROKER_CONFIG is given to ActiveMQ and it uses Java URI to parse the path, so we have to replace Windows path separator with Unix path separator
set ACTIVEMQ_BROKER_CONFIG=%ACTIVEMQ_BROKER_CONFIG:\\=/%

set ACTIVEMQ_STORAGE_DIR=%ACTIVEMQ_STORAGE_DIR:\\=\\\\%
set ACTIVEMQ_RAR=%ACTIVEMQ_RAR:\\=\\\\%
set WMS_LOGBACK_XML=%WMS_LOGBACK_XML::=\\:%
set WMS_CONFIG_DIR=%WMS_CONFIG_DIR::=\\:%
set ACTIVEMQ_BROKER_CONFIG=%ACTIVEMQ_BROKER_CONFIG::=\\:%
set ACTIVEMQ_REMOTE_CONNECTOR=%ACTIVEMQ_REMOTE_CONNECTOR::=\\:%
set ACTIVEMQ_STORAGE_DIR=%ACTIVEMQ_STORAGE_DIR::=\\:%

rem Defaults

if [%MAX_PERM_SIZE%]==[] set MAX_PERM_SIZE=400m
if [%INIT_PERM_SIZE%]==[] set INIT_PERM_SIZE=400m
if [%MAX_HEAP_SIZE%]==[] set MAX_HEAP_SIZE=2G

if [%MAIL_SERVER%]==[] set MAIL_SERVER=172.31.77.69
if [%MAIL_USER%]==[] set MAIL_USER=superuser@emea.tld
if [%MAIL_PASS%]==[] set MAIL_PASS=dcd3
if [%SMTP_PORT%]==[] set SMTP_PORT=25
if [%POP3_PORT%]==[] set POP3_PORT=110

if [%GLASSFISH_DEBUG_ENABLED%]==[] set GLASSFISH_DEBUG_ENABLED=false

rem Classic environment variables
if [%CLASSIC_SYSTEM_ROOT%]==[] set CLASSIC_SYSTEM_ROOT=%SCRIPT_DIR%
if x%CLASSIC_SYSTEM_ROOT::\=%==x%CLASSIC_SYSTEM_ROOT% set CLASSIC_SYSTEM_ROOT=%SCRIPT_DIR%%CLASSIC_SYSTEM_ROOT%
set CLASSIC_SYSTEM_ROOT=%CLASSIC_SYSTEM_ROOT:\\=/%
set CLASSIC_SYSTEM_ROOT=%CLASSIC_SYSTEM_ROOT::=\\:%

if [%CLASSIC_CONFIG_PATH%]==[] set CLASSIC_CONFIG_PATH=%SCRIPT_DIR%
if x%CLASSIC_CONFIG_PATH::\=%==x%CLASSIC_CONFIG_PATH% set CLASSIC_CONFIG_PATH=%SCRIPT_DIR%%CLASSIC_CONFIG_PATH%
set CLASSIC_CONFIG_PATH=%CLASSIC_CONFIG_PATH:\\=/%
set CLASSIC_CONFIG_PATH=%CLASSIC_CONFIG_PATH::=\\:%

if [%CLASSIC_CONFIG_ENV%]==[] set CLASSIC_CONFIG_ENV=%COMPUTERNAME%
if "%CLASSIC_PROJECT_PATH%" == "" set CLASSIC_PROJECT_PATH=config/mfc;config/eai;config/classic;config/com;target/classic/config/mfc;target/classic/config/eai;target/classic/config/classic;target/classic/config/com
if [%CLASSIC_RUNTIME_DATA%]==[] set CLASSIC_RUNTIME_DATA=../logs

exit /B 0
